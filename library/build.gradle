plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.4.21'
    id 'maven-publish'
}

group 'se.jsam.mp'
version '1.0'

repositories {
    mavenCentral()
}

kotlin {
    jvm()
    ios {
        binaries {
            framework {
                baseName = "DeviceInfo"
            }
        }
    }
    js {
        nodejs()
    }

    sourceSets {
        def ktor_version = "1.5.1"
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"
                implementation("io.ktor:ktor-client-core:$ktor_version")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation("io.ktor:ktor-client-apache:$ktor_version")
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
        iosMain {
            dependencies {
                implementation("io.ktor:ktor-client-ios:$ktor_version")
            }
        }
        jsMain {
            dependencies {
                implementation("io.ktor:ktor-client-js:$ktor_version")
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

tasks.register("assembleFramework", Exec) {
    dependsOn 'linkReleaseFrameworkIosArm64', 'linkReleaseFrameworkIosX64'
    executable "xcodebuild"
    args = [
            '-create-xcframework',
            '-framework',
            'build/bin/iosArm64/releaseFramework/DeviceInfo.framework',
            '-framework',
            'build/bin/iosX64/releaseFramework/DeviceInfo.framework',
            '-output',
            'build/framework/DeviceInfo.xcframework',
    ]
}